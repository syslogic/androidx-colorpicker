
task javadoc(type: Javadoc) {

    doFirst {
        configurations.implementation.filter { it.name.endsWith('.aar') }.each { aar ->
            copy {
                from zipTree(aar)
                include "**/classes.jar"
                into "$buildDir/tmp/aarsToJars/${aar.name.replace('.aar', '')}/"
            }
        }

        def aar_main = new File("$buildDir/intermediates/aar_main_jar")
        if (! aar_main.exists()) {
            // it depends on task :library:assembleDebug output
            throw new GradleException('Not found: ' + aar_main.getAbsolutePath().toString())
        } else {
            copy {
                from aar_main
                include "**/classes.jar"
                into "$buildDir/tmp/aarsToJars/aar_main_jar/"
            }
        }
    }

    failOnError false
    configurations.implementation.setCanBeResolved(true)
    title = "Colorpicker Library ${versionName} API"
    source = android.sourceSets.main.java.srcDirs
    classpath = files(new File("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"))
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += fileTree(dir: "$buildDir/tmp/aarsToJars/")
    classpath += configurations.implementation
    destinationDir = file("${project.buildDir}/outputs/javadoc/")
    exclude "**/BuildConfig.java", "**/R.java"

    options {
        author true
        linkSource true
        // verbose()
        links "https://docs.oracle.com/en/java/javase/11/docs/api/"
        linksOffline "https://developer.android.com/reference","${android.sdkDirectory}/docs/reference"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = "io.syslogic"
version = versionName

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "io.syslogic"
                artifactId = "androidx-colorpicker"
                from components.getByName('release')
                version = versionName
                pom {
                    name = 'Color Picker'
                    description = 'A simple color-picker library for Android'
                    url = "https://github.com/syslogic/${artifactId}"
                    scm {
                        connection = "scm:git:git://github.com/syslogic/${artifactId}.git"
                        developerConnection = "scm:git:ssh://github.com/syslogic/${artifactId}.git"
                        url = "https://github.com/syslogic/${artifactId}/"
                    }
                }
            }
        }
    }
}
