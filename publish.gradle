
task javadoc(type: Javadoc) {
    doFirst {
        configurations.implementation.filter { it.name.endsWith('.aar') }.each { aar ->
            copy {
                from zipTree(aar)
                include "**/classes.jar"
                into "$buildDir/tmp/aarsToJars/${aar.name.replace('.aar', '')}/"
            }
        }
    }
    failOnError false
    configurations.implementation.setCanBeResolved(true)
    title = "colorpicker ${versionName} API"
    source = android.sourceSets.main.java.srcDirs
    classpath = files(new File("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"))
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += fileTree(dir: "$buildDir/tmp/aarsToJars/")
    classpath += configurations.implementation
    destinationDir = file("${project.buildDir}/outputs/javadoc/")
    options.linkSource true
    options.links("https://docs.oracle.com/javase/8/docs/api/")
    options.links("https://developer.android.com/reference/")
    exclude "**/BuildConfig.java"
    exclude "**/R.java"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = "io.syslogic"
version = versionName

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "io.syslogic"
                artifactId = "androidx-colorpicker"
                version = versionName
                from components.release
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }
}
