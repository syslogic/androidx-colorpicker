// Module :library
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    namespace 'io.syslogic.colorpicker'
    buildToolsVersion = build_tools_version
    compileSdk compile_sdk
    defaultConfig {
        setProperty('archivesBaseName', 'colorpicker')
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles "${project.rootDir}/proguard/consumer.pro"
        targetSdk target_sdk
        minSdk min_sdk
    }
    
    sourceSets.main.java.srcDirs = [ "src/main/java" ]

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    composeOptions {
        // See the "Compose to Kotlin Compatibility Map" for compatible versions:
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion kotlin_compiler_extension_version
    }

    buildFeatures {
        buildConfig true
        dataBinding true
        compose true
    }

    buildTypes {
        debug {
            // it breaks the data-binding, eg. when running ./gradlew :library:publishToMavenLocal
            testCoverageEnabled false
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    lint {
        lintConfig file('../lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }

    packagingOptions {
        resources.pickFirsts.add('META-INF/LGPL2.1')
        resources.pickFirsts.add('META-INF/AL2.0')
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    implementation "com.google.android.material:material:$material_version"
    // implementation "com.google.android.material:compose-theme-adapter-3:1.0.1"

    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // https://mvnrepository.com/artifact/androidx.compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    // implementation "androidx.compose.material:material-icons-core:$compose_version"
    // implementation "androidx.compose.material:material-icons-extended:$compose_version"

    /* Composable Preview */
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0"
}

tasks.register('javadoc', Javadoc) {

    title = "Color Picker ${android.defaultConfig.versionName} API"
    source = android.sourceSets.main.java.srcDirs
    destinationDir = file("${project.buildDir}/outputs/javadoc/")
    configurations.implementation.setCanBeResolved(true)

    classpath = files(new File("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"))
    android.getBootClasspath().forEach{classpath += fileTree(it) }
    classpath += fileTree(dir: "$buildDir/tmp/aarsToJars/")
    classpath += configurations.implementation
    exclude "**/BuildConfig.java", "**/R.java", "**/*.kt"
    failOnError false

    options.verbose()
    // javadoc: warning - The code being documented uses modules but the packages
    // defined in https://developer.android.com/reference/ are in the unnamed module.
    options.links "https://docs.oracle.com/en/java/javase/17/docs/api/"
    options.linksOffline "https://developer.android.com/reference", "${android.sdkDirectory}/docs/reference"
    options.linkSource true
    options.author true

    doFirst {

        // extract AAR files
        configurations.implementation
                .filter { it.name.endsWith('.aar') }
                .each { aar ->
                    copy {
                        from zipTree(aar)
                        include "**/classes.jar"
                        into "$buildDir/tmp/aarsToJars/${aar.name.replace('.aar', '')}/"
                    }
                }

        // provide JAR, which contains the generated data-binding classes
        def aar_main = new File("$buildDir/intermediates/aar_main_jar")
        if (aar_main.exists()) {
            copy {
                from aar_main
                include "**/classes.jar"
                into "$buildDir/tmp/aarsToJars/aar_main_jar/"
            }
        }
    }
}

javadoc.onlyIf {
    new File("$buildDir/intermediates/aar_main_jar").exists()
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'io.syslogic'
version = version_name

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = 'androidx-colorpicker'
                from components.getByName('release')
                version = version_name
                pom {
                    name = 'Color Picker'
                    description = 'A simple color-picker library for Android'
                    url = "https://github.com/syslogic/${artifactId}"
                    scm {
                        connection = "scm:git:git://github.com/syslogic/${artifactId}.git"
                        developerConnection = "scm:git:ssh://github.com/syslogic/${artifactId}.git"
                        url = "https://github.com/syslogic/${artifactId}/"
                    }
                }
            }
        }
    }
}
