// Module :mobile
plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

base {
    archivesName = 'colorpicker_demo_' + version_name
}

android {
    namespace "io.syslogic.demo.colorpicker"
    buildToolsVersion = build_tools_version
    compileSdk compile_sdk
    defaultConfig {
        applicationId "io.syslogic.demo.colorpicker"
        testBuildType "debug"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode version_code
        versionName version_name
        targetSdk target_sdk
        minSdk min_sdk
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    composeOptions {
        // See the "Compose to Kotlin Compatibility Map" for compatible versions:
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion kotlin_compiler_extension_version
    }

    buildFeatures {
        buildConfig true
        dataBinding true
        compose true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    lint {
        lintConfig file('../lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }
}

dependencies {

    // Use either Jitpack repository or project module :library (local).
    // implementation "io.syslogic:androidx-colorpicker:$version_name"
    implementation project(path: ':library')

    implementation "com.google.android.material:material:$material_version"

    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.preference:preference-ktx:$androidx_preference_version"

    androidTestImplementation "androidx.navigation:navigation-testing:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-runtime-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"

    // https://mvnrepository.com/artifact/androidx.compose
    // https://developer.android.com/jetpack/compose/tooling#bom
    implementation platform("androidx.compose:compose-bom:$androidx_compose_bom_version")
    debugImplementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.activity:activity-compose"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"

    // jUnit
    testImplementation "junit:junit:$junit_version"

    // Required for connected tests.
    // https://mvnrepository.com/artifact/androidx.test/monitor
    debugImplementation "androidx.test:monitor:$androidx_test_monitor_version"

    // Required for connected tests (ActivityInvoker).
    // https://mvnrepository.com/artifact/androidx.test.ext
    debugImplementation "androidx.test.ext:junit:$androidx_test_junit_version"

    // https://mvnrepository.com/artifact/androidx.test
    // https://developer.android.com/jetpack/androidx/releases/test
    androidTestImplementation "androidx.test:core:$androidx_test_core_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_rules_version"

    // https://mvnrepository.com/artifact/androidx.test.uiautomator/uiautomator
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$androidx_test_uiautomator_version"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_test_espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-contrib:$androidx_test_espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-intents:$androidx_test_espresso_version"
    // androidTestImplementation "androidx.test.espresso:espresso-accessibility:$androidx_test_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-web:$androidx_test_espresso_version"
    // androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$androidx_test_espresso_version"

    // The following dependency can be either "implementation" or "androidTestImplementation",
    // depending on whether you want it to appear on your APK's compile classpath or the test APK classpath.
    // androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$androidx_test_espresso_version"

    // Test rules and transitive dependencies:
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.6.3"

    // Needed for createComposeRule, but not createAndroidComposeRule:
    //noinspection GradleDependency
    debugImplementation("androidx.compose.ui:ui-test-manifest")
}

// disable caching
configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
