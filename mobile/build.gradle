// Module :mobile
plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

base {
    archivesName = 'colorpicker_demo_' + version_name
}

android {
    namespace "io.syslogic.demo.colorpicker"
    buildToolsVersion = '34.0.0'
    compileSdk compile_sdk
    defaultConfig {
        applicationId "io.syslogic.demo.colorpicker"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testBuildType "debug"
        versionCode version_code
        versionName version_name
        targetSdk target_sdk
        minSdk min_sdk
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    composeOptions {
        // See the "Compose to Kotlin Compatibility Map" for compatible versions:
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion libs.versions.kotlin.compiler.extension.get()
    }

    buildFeatures {
        buildConfig true
        dataBinding true
        compose true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    lint {
        lintConfig file('../lint.xml')
        checkAllWarnings true
        warningsAsErrors true
        abortOnError false
        showAll false
    }
}

dependencies {

    // Use either Jitpack repository or project module :library (local).
    // implementation "io.syslogic:androidx-colorpicker:$version_name"
    implementation project(path: ':library')

    implementation libs.material.design

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.preference.ktx

    androidTestImplementation libs.androidx.navigation.testing
    implementation libs.androidx.navigation.runtime.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    // https://mvnrepository.com/artifact/androidx.compose
    // https://developer.android.com/jetpack/compose/tooling#bom
    implementation platform(libs.androidx.compose.bom)
    //noinspection UseTomlInstead
    debugImplementation "androidx.compose.ui:ui-tooling-preview"
    //noinspection UseTomlInstead
    implementation "androidx.activity:activity-compose"
    //noinspection UseTomlInstead
    implementation "androidx.compose.foundation:foundation"
    //noinspection UseTomlInstead
    implementation "androidx.compose.material:material"
    //noinspection UseTomlInstead
    implementation "androidx.compose.runtime:runtime"
    //noinspection UseTomlInstead
    implementation "androidx.compose.ui:ui"

    // jUnit
    testImplementation libs.junit

    // Required for connected tests.
    // https://mvnrepository.com/artifact/androidx.test/monitor
    debugImplementation libs.androidx.test.monitor

    // Required for connected tests (ActivityInvoker).
    // https://mvnrepository.com/artifact/androidx.test.ext
    debugImplementation libs.androidx.test.junit

    // https://mvnrepository.com/artifact/androidx.test
    // https://developer.android.com/jetpack/androidx/releases/test
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules

    // https://mvnrepository.com/artifact/androidx.test.uiautomator/uiautomator
    androidTestImplementation libs.androidx.test.uiautomator

    // Espresso
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.espresso.web
    // androidTestImplementation libs.androidx.espresso.idling.concurrent
    // androidTestImplementation libs.androidx.espresso.accessibility
    // androidTestImplementation libs.androidx.espresso.contrib
    // androidTestImplementation libs.androidx.espresso.intents

    // The following dependency can be either "implementation" or "androidTestImplementation",
    // depending on whether you want it to appear on your APK's compile classpath or the test APK classpath.
    // androidTestImplementation libs.androidx.espresso.idling.resource

    // Test rules and transitive dependencies:
    androidTestImplementation libs.androidx.compose.uitest

    // Needed for createComposeRule, but not createAndroidComposeRule:
    //noinspection UseTomlInstead,GradleDependency
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}

// disable caching
configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
